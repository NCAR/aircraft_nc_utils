# -*- python -*-

def asciidoc(env):
    if env['PLATFORM'] == 'darwin':
        man = Builder(action='a2x --no-xmllint --doctype manpage --format manpage $SOURCE',
                  src_suffix='.1.txt', suffix='.1')
        html = Builder(action='a2x --no-xmllint --doctype manpage --format xhtml $SOURCE',
                   src_suffix='.1.txt', suffix='.1.html')
    else:
        man = Builder(action='a2x --doctype manpage --format manpage $SOURCE',
                  src_suffix='.1.txt', suffix='.1')
        html = Builder(action='a2x --doctype manpage --format xhtml $SOURCE',
                   src_suffix='.1.txt', suffix='.1.html')

    env['BUILDERS']['AsciidocMan'] = man
    env['BUILDERS']['AsciidocHTML'] = html

tools=['default', 'netcdf', 'prefixoptions', 'buildmode', 'gsl',
       'boost_program_options', asciidoc]
env = Environment(tools=tools)

objs = env.StaticObject(Split("""NcCache.cc NcComparison.cc ReportStyle.cc"""))

nccompare = env.Program(["nc_compare.cc"] + objs)

manpage = env.AsciidocMan('nc_compare.1.txt')
env.AsciidocHTML('nc_compare.1.txt')

env.Default(nccompare)
env.Install('$INSTALL_PREFIX/bin', nccompare)
env.Install('$INSTALL_PREFIX/man/man1', manpage)

env = Environment(tools=tools+['testing', 'gtest_main'])

test_sources = Split("test_nccache.cc")

nt = env.Program('nc_compare_tests', test_sources + objs)

env.Alias('test', env.Test(nt, "cd ${SOURCE.dir} && ./${SOURCE.file} ${GTESTS}"))
