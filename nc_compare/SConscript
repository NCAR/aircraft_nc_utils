# -*- python -*-

from SCons.Script import Environment, Builder
import eol_scons


def asciidoc(env):
    if env['PLATFORM'] == 'darwin':
        man = Builder(action='a2x --no-xmllint --doctype manpage --format manpage $SOURCE',
                  src_suffix='.1.txt', suffix='.1')
        html = Builder(action='a2x --no-xmllint --doctype manpage --format xhtml $SOURCE',
                   src_suffix='.1.txt', suffix='.1.html')
    else:
        man = Builder(action='a2x --doctype manpage --format manpage $SOURCE',
                  src_suffix='.1.txt', suffix='.1')
        html = Builder(action='a2x --doctype manpage --format xhtml $SOURCE',
                   src_suffix='.1.txt', suffix='.1.html')

    env['BUILDERS']['AsciidocMan'] = man
    env['BUILDERS']['AsciidocHTML'] = html


tools = ['default', 'netcdf', 'prefixoptions', 'buildmode', 'gsl',
         'boost_program_options', asciidoc]
env = Environment(tools=tools)

# nc_compare now requires C++11
env.AppendUnique(CXXFLAGS=['-std=c++11'])

objs = env.StaticObject(env.Split("""NcCache.cc NcComparison.cc ReportStyle.cc"""))

nccompare = env.Program(["nc_compare.cc"] + objs)

manpage = env.AsciidocMan('nc_compare.1.txt')
env.AsciidocHTML('nc_compare.1.txt')

env.Default(nccompare)
env.Alias('install.nc_compare', env.Install('$INSTALL_PREFIX/bin', nccompare))
env.Install('$INSTALL_PREFIX/man/man1', manpage)

# gtest now requires C++14
env = Environment(tools=tools+['testing', 'gtest_main'])
env.AppendUnique(CXXFLAGS=['-std=c++14'])

test_sources = env.Split("test_nccache.cc")

nt = env.Program('nc_compare_tests', test_sources + objs)

env.Alias('test', env.Test(nt, "cd ${SOURCE.dir} && ./${SOURCE.file} ${GTESTS}"))
